buildscript {
	ext {
		springBootVersion = '1.5.7.RELEASE'
	}
	repositories {
		mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'org.postgresql:postgresql:9.4.1212'
        classpath "nu.studer:gradle-jooq-plugin:2.0.7"
        classpath "gradle.plugin.com.boxfuse.client:flyway-release:4.2.0"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: "nu.studer.jooq"
apply plugin: "org.flywaydb.flyway"

group = 'ru.hixon'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-actuator-docs')
    compile group: 'org.flywaydb', name: 'flyway-core', version: '4.2.0'
    compile('org.springframework.boot:spring-boot-starter-jooq')
    compile('org.springframework.boot:spring-boot-starter-web')
    runtime('org.springframework.boot:spring-boot-devtools')
    testCompile('org.springframework.boot:spring-boot-starter-test')

    compile('org.postgresql:postgresql:9.4.1212')
    jooqRuntime 'org.postgresql:postgresql:9.4.1212'
}


jooq {
    csmart(sourceSets.main) {
        jdbc {
            driver = 'org.postgresql.Driver'
            url = 'jdbc:postgresql://localhost:5432/blog'
            user = 'spring-boot-user'
            password = 'spring-boot-user'
            schema = 'public'
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.postgres.PostgresDatabase'
                inputSchema = 'public'
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = false
                fluentSetters = true
            }
            target {
                packageName = 'ru.hixon.gen'
                directory = 'src/main/generated/java'
            }
        }
    }
}

flyway {
    url = 'jdbc:postgresql://localhost:5432/blog'
    user = 'spring-boot-user'
    password = 'spring-boot-user'
    schemas = ['public']
    locations = ["filesystem:$project.projectDir/src/main/resources/db/migration"]
}

build.dependsOn(test)

generateCsmartJooqSchemaSource.dependsOn flywayMigrate